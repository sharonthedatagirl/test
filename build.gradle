/*
 * Build ippo assignment1
 *
 * ** YOU MUST EDIT THE 'uid' VARIABLE BELOW TO YOUR OWN UID **
 *
 * common tasks are:
 * 'gradle run'        - build and run the code
 * 'gradle runX'       - build and run the code simulating bug X
 * 'gradle test'       - build and run the tests
 * 'gradle testX'      - build and run the tests simulating bugX
 *
 */

apply plugin: 'java'

/*********************************************************************************************
 * user id
 ********************************************************************************************/

// the 'uid' is used when running the BuggyProxy
// you must replace the 'NONE' with your own user-id (eg. 's12345678')

def uid = 'NONE'

/*********************************************************************************************
 * dependencies
 ********************************************************************************************/

// this specifies the libraries that the application requires, and the online
// repositories where they are found. gradle will automatically download these
// as required, so you do not need to manually download any library code.

repositories {
    mavenCentral()
    maven { url = "https://ease.groups.inf.ed.ac.uk/ippo/2020/repo" }
}

dependencies { compile "uk.ac.ed.inf:ippo-library:+" }

def currentOS = org.gradle.internal.os.OperatingSystem.current()
def platform
if (currentOS.isWindows()) {
    platform = 'win'
} else if (currentOS.isLinux()) {
    platform = 'linux'
} else if (currentOS.isMacOsX()) {
    platform = 'mac'
}

dependencies {
    compile "org.openjfx:javafx-base:14:${platform}"
    compile "org.openjfx:javafx-graphics:14:${platform}"
    compile "org.openjfx:javafx-controls:14:${platform}"
    compile "org.openjfx:javafx-fxml:14:${platform}"
}

/*********************************************************************************************
 * execution
 ********************************************************************************************/

// if you change the name of the main class, you need to change this
// otherwise, you probably don't want to change anything here

def mainClassName='ippo.assignment1.Main'

task run(type: JavaExec) {
  group = "ippo"
  description = "run application"
  environment "IPPO_UID", "${uid}"
  classpath sourceSets.main.runtimeClasspath
  main = "${mainClassName}"
}

/*********************************************************************************************
 * testing
 ********************************************************************************************/

// this specifies the version of JUnit to use for testing

dependencies { testImplementation 'junit:junit:4.12' }

test {
  group = "ippo"
  description = "run tests"
  environment "IPPO_UID", "${uid}"
  exclude '**/BuggyProxyTest*'
  useJUnit()
}

/*********************************************************************************************
 * simulate bugs in the BuggyProxy
 ********************************************************************************************/

('A'..'G').each {
  def bug = it
  def taskName = "runCase"+bug
  task(taskName,type: JavaExec) {
    group = "ippo (run buggy proxy)"
    description = "run proxy with case "+bug
    environment "IPPO_BUG", "${bug}"
    environment "IPPO_UID", "${uid}"
    classpath sourceSets.main.runtimeClasspath
    main = "${mainClassName}"
  }
  def testName = "testCase"+bug
  task(testName,type: Test) {
    group = "ippo (test buggy proxy)"
    description = "test proxy with case "+bug
    environment "IPPO_BUG", "${bug}"
    environment "IPPO_UID", "${uid}"
    include '**/BuggyProxyTest*'
    useJUnit()
  }
}
